plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id("com.github.johnrengelman.shadow") version "8.1.1" // <- Shadow plugin
}

group = 'com.hmmbo'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    compileOnly("org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT")
    implementation 'io.github.revxrsal:lamp.common:4.0.0-rc.12'
    implementation 'io.github.revxrsal:lamp.bukkit:4.0.0-rc.12'
}

tasks {
    runServer {
        minecraftVersion("1.16.5")
    }

    shadowJar {
        archiveClassifier.set("") // Makes the shaded jar be the main output (no -all suffix)
        relocate "io.github.revxrsal.lamp", "com.hmmbo.shaded.lamp" // Optional: Prevents class conflicts
    }

    build {
        dependsOn(shadowJar)
    }
}

def targetJavaVersion = 16
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion.set(JavaLanguageVersion.of(targetJavaVersion))
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}
